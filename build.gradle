apply plugin: 'groovy'
apply plugin: 'project-report'
apply plugin: 'jacoco'

ext {
    groovyVersion = '2.2.0'
    hamcrestVersion = '1.3'
    junitVersion = '4.11'
    logbackVersion = '1.0.13'
    slf4jVersion = '1.7.5'
    spockVersion = '0.7-groovy-2.0'
    jacocoVersion = '0.6.3.201306030806'
}

defaultTasks 'clean', 'build', 'projectReport', 'ronbo'

repositories {
    mavenCentral()
}

configurations.all {
    exclude group: 'commons-logging'
    exclude group: 'log4j'
    exclude module: 'junit-dep'
    exclude module: 'hamcrest-core'
}

jacoco {
    toolVersion = jacocoVersion
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled true
        html.enabled true
    }
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    compile "org.slf4j:slf4j-api:${slf4jVersion}"
    compile "org.slf4j:slf4j-ext:${slf4jVersion}"
    compile "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
    compile "ch.qos.logback:logback-core:${logbackVersion}"
    compile "ch.qos.logback:logback-classic:${logbackVersion}"

    runtime "org.slf4j:jul-to-slf4j:${slf4jVersion}"
    runtime "org.slf4j:log4j-over-slf4j:${slf4jVersion}"

    testCompile "org.spockframework:spock-core:${spockVersion}"
    testCompile "junit:junit:${junitVersion}"
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.9'
}

task copyTask( type: Copy, dependsOn: 'jacocoTestReport' ) {
    from 'report.dtd'
    into 'build/reports/jacoco/test'
}

task ronbo( dependsOn: 'copyTask' ) {

    doFirst {
        def xml = new XmlParser().parse( 'build/reports/jacoco/test/jacocoTestReport.xml' )
        if ( xml.package.class.counter.findAll{ it.@type == 'CLASS' }.collect { it.@missed >= it.@covered }.any { it } ) {
            throw new IllegalStateException( 'Not enough code coverage! Consult build/reports/jacoco/test/html/index.html for details.' )
        }
        else {
            logger.quiet 'Adequate code coverage detected.'
        }
    }
}
